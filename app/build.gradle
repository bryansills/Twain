apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'

android {
    compileSdkVersion 22
    buildToolsVersion "22.0.1"

    def versionMajor = 0
    def versionMinor = 1
    def versionPatch = 0

    defaultConfig {
        applicationId "ninja.bryansills.twain"
        minSdkVersion 16
        targetSdkVersion 22

        versionCode getTimestamp()
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    signingConfigs {
        release {
            try {
                /* Place the below properties in a 'gradle.properties' file in the 'app/' folder */
                storeFile file(KEYSTORE)
                storePassword KEYSTORE_PASSWORD
                keyAlias KEY_ALIAS
                keyPassword KEY_PASSWORD
            }
            catch (ex) {
                println("-------------------------------------------------------------")
                println("Release build signing is not set up on this machine. Release builds will fail.")
                println("-------------------------------------------------------------")
                println(ex)
                println("-------------------------------------------------------------")
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:appcompat-v7:22.0.0'
    compile 'com.android.support:support-v4:22.0.0'

    compile 'com.google.dagger:dagger:2.0'
    apt 'com.google.dagger:dagger-compiler:2.0'
    provided 'org.glassfish:javax.annotation:10.0-b28'
}

def getTimestamp() {
    TimeZone.setDefault(TimeZone.getTimeZone('UTC'))
    def now = new Date()
    def formattedDate = now.format('yyMMddHHmm')
    def code = formattedDate.toInteger()
    return code
}
